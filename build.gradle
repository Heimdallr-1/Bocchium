import net.darkhax.curseforgegradle.TaskPublishCurseForge
import net.darkhax.curseforgegradle.UploadArtifact

plugins {
    id "dev.architectury.loom" version "1.1-SNAPSHOT"
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
            "bocchium.mixins.json"
        ]
    }
    mixin.defaultRefmapName.set("bocchium.refmap.json")
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "curse.maven:embeddium-908741:4984830"
    forge "net.minecraftforge:forge:${project.forge_version}"
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

String[] supportedGameVersions = new String[]{"${minecraft_version}"}
String currentChangeLog = file("CHANGELOG.md").getText("UTF-8");

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${archives_base_name}"
    versionNumber = "${mod_version}"
    uploadFile = remapJar
    gameVersions.addAll(supportedGameVersions)
    loaders = ["forge"]
    versionType = "release"
    additionalFiles = [sourcesJar]
    changelog.set(currentChangeLog)
}

tasks.register("publishCurseForge", TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    disableVersionDetection()

    UploadArtifact mainFile = upload(cf_project_id, remapJar)
    mainFile.displayName = "${archives_base_name}-${version}"
    mainFile.releaseType = "release"
    mainFile.changelog = currentChangeLog
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addJavaVersion("Java 17", "Java 8")
    mainFile.addGameVersion(supportedGameVersions)

    UploadArtifact sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog = currentChangeLog
    sourcesFile.changelogType = "markdown"
}